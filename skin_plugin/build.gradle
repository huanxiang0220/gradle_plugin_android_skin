plugins {
    // 引入kotlin
    id 'org.jetbrains.kotlin.jvm'
    // 添加 java-gradle-plugin 以支持作为 Gradle 插件使用
    id 'java-gradle-plugin'
}

targetCompatibility = JavaVersion.VERSION_11
sourceCompatibility = JavaVersion.VERSION_11

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "11"
    }
}

// agp对应的其它依赖版本
def agpDependencies = [
        '8.7.0': [
                'com.android.tools.build:gradle:8.7.0',
                'com.android.tools:common:31.7.0',
                'commons-io:commons-io:2.15.1'
        ],
        '7.2.0': [
                'com.android.tools.build:gradle:7.2.0',
                'com.android.tools:common:30.2.0',
                'commons-io:commons-io:2.6'
        ]
]

dependencies {

    // 智能依赖策略：根据当前 JVM 版本决定依赖哪个版本的 AGP
    // 如果是 Java 17+ 环境（通常用于 AGP 8.x），则引入 AGP 8.x 依赖以便编译
    // 如果是 Java 11 环境（通常用于 AGP 7.x），则只引入 AGP 7.x 依赖，避免 "Requires Java 17" 错误
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
        println "SkinPlugin Build: Current JVM is ${JavaVersion.current()}, including AGP 8.7.0 dependencies."
        agpDependencies['8.7.0'].each {
            compileOnly(it)
        }
    } else {
        println "SkinPlugin Build: Current JVM is ${JavaVersion.current()}, skipping AGP 8.7.0 dependencies to ensure compatibility."
    }
    
    // 始终兼容 AGP 7.x (Java 11+)
    agpDependencies['7.2.0'].each {
        compileOnly(it)
    }

}


// 引用发布到maven插件
apply from: 'plugin_maven_publish.gradle'